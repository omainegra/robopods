buildscript {
    ext.versions = [
        "kotlin" : "1.2.21",
        "robovm" : "2.3.4-SNAPSHOT",
    ]

    repositories {
        jcenter()
        mavenCentral()
        maven { url "http://oss.sonatype.org/content/repositories/snapshots/" }
    }

    dependencies {
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3"
        classpath "com.mobidevelop.robovm:robovm-gradle-plugin:${versions.robovm}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
    }
}

def pods = ["sdwebimage","lottie"]

configure(pods.collect { project(":$it") }) {
    apply plugin: "java"
    apply plugin: 'maven'
    apply plugin: "maven-publish"
    apply plugin: "com.jfrog.bintray"

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    dependencies {
        compile "com.mobidevelop.robovm:robovm-rt:${versions.robovm}"
        compile "com.mobidevelop.robovm:robovm-cocoatouch:${versions.robovm}"
    }

    task generatePod(type: Exec) { task ->
        String bro = "${task.project.rootProject.rootDir}/tools/robovm-bro-gen/bro-gen.rb"
        commandLine "$bro", "src/main/java/", "src/main/bro-gen/${task.project.name}.yaml"
    }
    compileJava.dependsOn(generatePod)

    task sourceJar(type: Jar) {
        from sourceSets.main.allJava
        classifier "sources"
    }

    // Create the pom configuration:
    def pomConfig = {
        licenses {
            license {
                name "The Apache Software License, Version 2.0"
                url "http://www.apache.org/licenses/LICENSE-2.0.txt"
                distribution "repo"
            }
        }
        developers {
            developer {
                id "omainegra"
                name "Omar Alejandro Mainegra Sarduy"
                email "omainegra@gmail.com"
            }
        }

        scm {
            url "https://github.com/omainegra/robopods.git"
        }
    }
    publishing {
        publications {
            MyPublication(MavenPublication) {
                from components.java
                artifact sourceJar
                groupId 'com.omainegra.robopods'
                artifactId project.name
                version project.version
                pom.withXml {
                    def root = asNode()
                    root.appendNode('description', 'Robovm Pods')
                    root.appendNode('name', 'Robopods')
                    root.appendNode('url', 'https://github.com/omainegra/robopods')
                    root.children().last() + pomConfig
                }
            }
        }
    }

    bintray {
        user = System.getenv('BINTRAY_USER')
        key = System.getenv('BINTRAY_KEY')
        publications = ['MyPublication']
        pkg {
            repo = 'robopods'
            name = project.name
            licenses = ['Apache-2.0']
            vcsUrl = 'https://github.com/omainegra/robopods.git'
        }
    }

    repositories {
        jcenter()
        mavenCentral()
        maven { url "http://oss.sonatype.org/content/repositories/snapshots/" }
    }
}